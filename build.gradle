subprojects { project ->
    group = GROUP
    version = VERSION_NAME


    repositories {
        mavenLocal()
        jcenter()
    }

    apply plugin: 'checkstyle'

    task checkstyle(type: Checkstyle) {
        configFile rootProject.file('checkstyle.xml')
        source 'src/main/java'
        ignoreFailures false
        showViolations true
        include '**/*.java'

        classpath = files()
    }

    afterEvaluate {
        if (project.tasks.findByName('check')) {
            check.dependsOn('checkstyle')
        }
    }

    buildscript {
        repositories {
            mavenLocal()
            jcenter()
        }
        dependencies {
            classpath 'com.android.tools.build:gradle:1.5.0'
            classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
        }
    }
}

ext {
    minSdkVersion = 8
    targetSdkVersion = 23
    compileSdkVersion = 23
    buildToolsVersion = '23.0.2'
    sourceCompatibilityVersion = JavaVersion.VERSION_1_7
    targetCompatibilityVersion = JavaVersion.VERSION_1_7
}

ext.deps = [
        // Android
        android           : 'com.google.android:android:2.1.2',
        supportv4         : 'com.android.support:support-v4:23.2.1',
        supportAnnotations: 'com.android.support:support-annotations:23.2.1',
        appCompat         : 'com.android.support:appcompat-v7:23.2.1',

        commonsLang       : 'org.apache.commons:commons-lang3:3.4',
        javapoet          : 'com.squareup:javapoet:1.3.0',

        // Test dependencies
        junit             : 'junit:junit:4.12',
        truth             : 'com.google.truth:truth:0.27',
        robolectric       : 'org.robolectric:robolectric:2.2',
        compiletesting    : 'com.google.testing.compile:compile-testing:0.7',
        autoservice       : 'com.google.auto.service:auto-service:1.0-rc2',
        autocommon        : 'com.google.auto:auto-common:0.4'
]
